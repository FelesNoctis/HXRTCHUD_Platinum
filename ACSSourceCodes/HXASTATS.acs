#library "HXASTATS"
#include "zcommon.acs"
#import "HXATOKEN.acs"
#import "HXAMODCM.acs"

// General
	#define healthtoken "HXSYS_HealthOverMaxToken"
	#define armortoken "HXSYS_ArmorOverMaxToken"
	#define staminatoken "HXSYS_StaminaToken"
	#define staminastickytoken "HXSYS_StaminaStickyToken"
	#define altitudetoken "HXSYS_AltitudeMetersToken"
	#define hudfadetime "hxcus_HXFadeTimeSeconds"

script "HXS_PlayerStats7ticHandler" enter
{
	// Wait 35 tics (1sec) before engaging, to make sure tokens have been given
	delay(35);

	if (!CheckInventory(HXS_GetTokenString(HXMOD_OMNIWOLF)))
	{
		while (true)
		{

			HXS_StatSync(GetActorProperty(0, APROP_Health), healthtoken);
			HXS_StatSync(CheckInventory("BasicArmor"), armortoken);
			HXS_PlayerAltitude();

			// Wait 7 tics (1/5sec) before repeating, since we want this to be reasonably responsive
			delay(7);
		}
	}
}

function void HXS_PlayerAltitude (void)
{
	int hxaltitude = ((GetActorZ (0) - GetActorFloorZ (0)) >> 16) / 42;
	int tokencount = CheckInventory(altitudetoken);

	HXS_ItemManagement(hxaltitude, altitudetoken, tokencount);
}

// This function should be called from a 7-tic handler
function void HXS_StamFlip (int usedstamina, str token, int maxstamina)
{
	int tokencorrection = maxstamina - usedstamina; // the HX stamina tokens we should have
	int tokencount = CheckInventory(token); // the HX stamina tokens we do have
	int stickytoken = CheckInventory(staminastickytoken);
	int stickytime = 5 * GetCVar(hudfadetime); // 5 x 7tics = 35tics, or 1 second

	if (usedstamina)
	{
		if (!stickytoken) { GiveInventory(staminastickytoken, stickytime); }
		if (tokencorrection > tokencount) { GiveInventory(token, tokencorrection - tokencount); }
		else if (tokencorrection < tokencount) { TakeInventory(token, tokencount - tokencorrection); }
	}
	else if (!usedstamina && tokencount != maxstamina) { GiveInventory(token, maxstamina); }
	else if (!usedstamina && tokencount == maxstamina && stickytoken) { TakeInventory(staminastickytoken, 1); }
}

// This function should be called from a 7-tic handler
function void HXS_StamNormal (str token, int maxstamina)
{
	int tokencount = CheckInventory(token);
	int stickytoken = CheckInventory(staminastickytoken);
	int stickytime = 5 * GetCVar(hudfadetime); // 5 x 7tics = 35tics, or 1 second

	if (tokencount && tokencount != maxstamina) { GiveInventory(staminastickytoken, stickytime); }
	else if (stickytoken && (tokencount == maxstamina || !tokencount)) { TakeInventory(staminastickytoken, 1); }
}

function void HXS_StatSync (int stat, str token)
{
	int normalmax = 100;
	int overstat = stat - normalmax;
	int tokencount = CheckInventory(token);

	HXS_ItemManagement(overstat, token, tokencount);
}