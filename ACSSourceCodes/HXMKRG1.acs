#library "HXMKRG1"
#include "zcommon.acs"
#import "HXATOKEN.acs"
#import "HXAMODCM.acs"

str kriegsland1_berserk = "SuperBlow";
str kriegsland1_crossmachineguns = "HDCrossAkimbo3";
str kriegsland1_crosspistols = "HDCrossAkimbo1";
str kriegsland1_crossthermals = "HDCrossAkimbo5";

script "HXS_Kriegsland1_35ticHandler" enter
{
	// Wait 35 tics (1sec) before engaging, to make sure the token has been given
	delay(35);

	if (CheckInventory(HXS_GetTokenString(HXMOD_KRIEGSLAND1))) {
		while (true)
		{
/* 		// Key Management Tokens
			// Blue Keys
			HXS_ItemSync(vanillabluecard, hxgen_bluecard);
			HXS_ItemSync(vanillablueskull, hxgen_blueskull);
			// Red Keys
			HXS_ItemSync(vanillaredcard, hxgen_redcard);
			HXS_ItemSync(vanillaredskull, hxgen_redskull);
			// Yellow Keys
			HXS_ItemSync(vanillayellowcard, hxgen_yellowcard);
			HXS_ItemSync(vanillayellowskull, hxgen_yellowskull); */

		// Powerup Management Tokens
			// Berserk
			HXS_ItemSync(kriegsland1_berserk, HXS_GetTokenString(HXGEN_BERSERK));

		// Timer
			// Wait 35 tics (~1sec) before repeating, since we want this to be a clock
			delay(35);
		}
	}
}

script "HXS_Kriegsland1_7ticHandler" enter
{
	// Wait 35 tics (1sec) before engaging, to make sure the token has been given
	delay(35);
	
	if (CheckInventory(HXS_GetTokenString(HXMOD_KRIEGSLAND1))) {
		while (true)
		{
		// Powerup Management Tokens
			// Infrared
			HXS_PowerupTimer(HXS_GetTokenString(VANILLAINFRARED), HXS_GetTokenString(HXGEN_INFRARED));
			// Invisibility - Removed
			/* HXS_PowerupTimer(HXS_GetTokenString(VANILLAINVISIBILITY), HXS_GetTokenString(HXGEN_INVISIBILITY)); */
			// Invulnerability
			HXS_PowerupTimer(HXS_GetTokenString(VANILLAINVULNERABILITY), HXS_GetTokenString(HXGEN_INVULNERABILITY));
			// Radsuit
			HXS_PowerupTimer(HXS_GetTokenString(VANILLARADSUIT), HXS_GetTokenString(HXGEN_RADSUIT));

		// Unique Ammo Dual Wield Check
			if ((CheckWeapon(kriegsland1_crosspistols)
				|| CheckWeapon(kriegsland1_crossmachineguns)
				|| CheckWeapon(kriegsland1_crossthermals))
				&& !CheckInventory(HXS_GetTokenString(HXMOD_WEAPONDUALWIELDUNIQUEAMMO)))
			{ GiveInventory(HXS_GetTokenString(HXMOD_WEAPONDUALWIELDUNIQUEAMMO), 1); }
			else if ((!CheckWeapon(kriegsland1_crosspistols)
				&& !CheckWeapon(kriegsland1_crossmachineguns)
				&& !CheckWeapon(kriegsland1_crossthermals))
				&& CheckInventory(HXS_GetTokenString(HXMOD_WEAPONDUALWIELDUNIQUEAMMO)))
			{ TakeInventory(HXS_GetTokenString(HXMOD_WEAPONDUALWIELDUNIQUEAMMO), 1); }

		// Timer
			// Wait 7 tics (1/5sec) before repeating, since we want this to be reasonably responsive
			delay(7);
		}
	}
}